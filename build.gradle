/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2018 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'groovy'
    id 'idea'
    id 'java-gradle-plugin'
    id 'org.kordamp.gradle.kordamp-parentpom' version '1.1.2'
    id 'com.gradle.plugin-publish'            version '0.10.0'
    id 'com.gradle.build-scan'      version '2.1'
}

buildScan {
    termsOfServiceUrl   = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

config {
    info {
        name          = rootProject.name
        description   = pluginDescription
        inceptionYear = '2017'
        tags          = ['jdeps', 'jdktool', 'jigsaw', 'modules']

        specification { enabled = false }
    }

    groovydoc {
        replaceJavadoc = true
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

dependencies {
    compile gradleApi()
    compile "org.zeroturnaround:zt-exec:$ztexecVersion"
}

compileGroovy {
    sourceCompatibility = project.sourceCompatibility
    targetCompatibility = project.targetCompatibility
}

task publishRelease(dependsOn: [bintrayUpload, publishPlugins]) {}

afterEvaluate {
    gradlePlugin {
        plugins {
            jdepsPlugin {
                id                  = pluginId
                implementationClass = pluginImplementationClass
            }
        }
    }

    bintray {
        pkg {
            version {
                attributes = ['gradle-plugin': "${project.pluginId}:${project.group}:${project.name}"]
            }
        }
    }

    pluginBundle {
        website     = project.mergedConfiguration.info.links.website
        vcsUrl      = project.mergedConfiguration.info.links.scm
        description = project.mergedConfiguration.info.description
        tags        = project.mergedConfiguration.info.tags

        plugins {
            jdepsPlugin {
                id          = project.pluginId
                displayName = project.mergedConfiguration.info.description
            }
        }

        mavenCoordinates {
            groupId    = project.group
            artifactId = project.name
            version    = project.version
        }
    }
}
